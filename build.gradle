ext {
	info = [
		name: "cpup-mc",
		group: "cpup.mc.lib",
		version: "dev",
		dependencies: [
			"cpup.lib:cpup-lib:dev",
			"mcp.mobius.waila:Waila:1.7.0-B2_1.9"
		]
	]

	versions = [
		scala: "2.11.1",
		minecraft: "1.9",
		forge: "12.16.0.1863"
	]
}

buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}

	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
	}
}

apply plugin: "java"

repositories {
	mavenLocal()
	mavenCentral()

	maven {
		name "ProfMobius"
		url "http://mobiusstrip.eu/maven"
	}
}

configurations {
	compile.extendsFrom exportedCompile
}

dependencies {
	info.dependencies.each { dep ->
		compile dep
	}
	exportedCompile "org.apache.logging.log4j:log4j-slf4j-impl:2.0-beta9"
}

jar {
	from { configurations.exportedCompile.collect { it.isDirectory() ? it : zipTree(it) } }
}

apply plugin: "scala"
dependencies.compile "org.scala-lang:scala-compiler:$versions.scala"
dependencies.compile "org.scala-lang:scala-library:$versions.scala"
dependencies.compile "org.scala-lang:scala-reflect:$versions.scala"

apply plugin: "net.minecraftforge.gradle.forge"
minecraft {
	version = "$versions.minecraft-$versions.forge-$versions.minecraft"
	runDir = "run"
	mappings = "snapshot_20160312"
	useDepAts = true
}

apply plugin: "maven"

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        expand version: info.version, mcVersion: versions.minecraft
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

archivesBaseName = info.name
group = info.group
version = info.version
